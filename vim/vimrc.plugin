
if has("vim_starting")
  set runtimepath+=~/.vim/bundle/neobundle.vim
endif

filetype plugin on
filetype plugin indent on " required!
filetype indent on

call neobundle#begin(expand('~/.vim/bundle/'))
  " NeoBundleをNeoBundle自体で管理する
  NeoBundleFetch 'Shougo/neobundle.vim'
  " Emmet
  NeoBundle 'mattn/emmet-vim'
  " 補完
  NeoBundle 'Shougo/neocomplete.vim'
  " スニペット
  NeoBundle 'Shougo/neosnippet'
  NeoBundle 'Shougo/neosnippet-snippets'
  " ファイラー的な
  NeoBundle 'Shougo/unite.vim'
  " Uniteで関数一覧などのアウトラインを表示する
  NeoBundle 'Shougo/unite-outline'
  " ifやdoを入力したら自動的にendを挿入してくれる
  NeoBundle 'tpope/vim-endwise'
  " vimでGitを扱う
  NeoBundle 'tpope/vim-fugitive'
  " Gitの変更を見やすくする
  NeoBundle 'airblade/vim-gitgutter'
  " :RTとかで対応するviewとかに移動できる
  NeoBundle 'tpope/vim-rails'
  " テキストを特定の文字で囲んだりする
  NeoBundle 'tpope/vim-surround'
  " trueとfalseをきりかえたり
  NeoBundle 'AndrewRadev/switch.vim'
  " 視覚的にreplaceできるやつ
  NeoBundle 'osyo-manga/vim-over'
  " 選択範囲を外側に広げて検索できる
  NeoBundle 'terryma/vim-expand-region'
  "行単位でdiffをみるためのプラグイン
  NeoBundle 'AndrewRadev/linediff.vim'
  " シンタックスエラーを見つけてくれるやつ
  NeoBundle 'scrooloose/syntastic.git'
  " 画面上の見えている範囲の移動を早くするためのプラグイン
  NeoBundle 'Lokaltog/vim-easymotion'
  " = の位置とかそろえられるやつ！
  NeoBundle 'junegunn/vim-easy-align'
  " fの検索機能を強化する
  NeoBundle 'rhysd/clever-f.vim'
  " ステータスラインをかっこよく
  NeoBundle 'itchyny/lightline.vim'
  " コーディングのログを収集してくれるサービス
  NeoBundle 'wakatime/vim-wakatime'

  NeoBundle 'Shougo/vimproc', {
    \ 'build' : {
      \ 'mac' : 'make -f make_mac.mak',
      \ 'unix' : 'make -f make_unix.mak',
    \ },
  \ }

  " ==================================================
  " テキストオブジェクト
  " ==================================================
  " テキストオブジェクト系のプラグインが依存しているプラグイン
  NeoBundle 'kana/vim-textobj-user'
  " [ al, il ] カーソル行
  NeoBundle 'kana/vim-textobj-line'
  " [ ae, ie ] バッファ全体
  NeoBundle 'kana/vim-textobj-entire'
  " [ au, iu ] Url
  NeoBundle 'mattn/vim-textobj-url'
  " [ ac, ic ] コメント
  NeoBundle "thinca/vim-textobj-comment"
  " [ a, , i, ] 引数
  NeoBundle "sgur/vim-textobj-parameter"

  " ==================================================
  " シンタックスハイライト
  " ==================================================
  " Coffee
  NeoBundle 'kchmck/vim-coffee-script', {'autoload':{'filetypes':['coffee']}}
  " Scala
  NeoBundle 'derekwyatt/vim-scala'
  " React
  NeoBundle 'pangloss/vim-javascript'
  NeoBundle 'mxw/vim-jsx'
  " haml
  NeoBundle 'tpope/vim-haml'

call neobundle#end()

" ==================================================
" Unite
" ==================================================

" Prefix
nnoremap [unite] <nop>
nmap <space>u [unite]

let g:unite_enable_start_insert = 1
let g:unite_enable_split_vertically = 0
let g:unite_winwidth = 40

" 通常のUnite
nnoremap <silent> [unite]f :<C-u>UniteWithBufferDir -tab -buffer-name=files file<CR>
nnoremap <silent> [unite]o :<C-u>Unite outline<CR>

" Ruby
" Controller
nnoremap <silent> [unite]co :call UniteFileRec('/app/controllers')<CR>
" Model
nnoremap <silent> [unite]mo :call UniteFileRec('/app/models')<CR>
" View
nnoremap <silent> [unite]vi :call UniteFileRec('/app/views')<CR>
" Helper
nnoremap <silent> [unite]he :call UniteFileRec('/app/helpers')<CR>
" Css
nnoremap <silent> [unite]cs :call UniteFileRec('/app/assets/stylesheets')<CR>
nnoremap <silent> [unite]fc :call UniteFileRec('/app/frontend/stylesheets')<CR>
" JavaScript
nnoremap <silent> [unite]js :call UniteFileRec('/app/assets/javascripts')<CR>
nnoremap <silent> [unite]fj :call UniteFileRec('/app/frontend/javascripts')<CR>
" Config
nnoremap <silent> [unite]cf :call UniteFileRec('/config')<CR>
" Lib
nnoremap <silent> [unite]li :call UniteFileRec('/lib')<CR>
" Spec
nnoremap <silent> [unite]sp :call UniteFileRec('/spec')<CR>
" Db
nnoremap <silent> [unite]db :call UniteFileRec('/db')<CR>
" Test
nnoremap <silent> [unite]tj :call UniteFileRec('/test/js/unit')<CR>

" Grep
nnoremap <silent> [unite]g :<C-u>Unite -tab grep -buffer-name=grep<CR>
if executable('hw')
  let g:unite_source_grep_command = 'hw'
  let g:unite_source_grep_default_opts = '--no-group --no-color'
  let g:unite_source_grep_recursive_opt = ''
endif

function! UniteFileRec(path)
  call unite#start#standard([[ 'file_rec/async', b:rails_root.a:path ]], {"tab": 1})
endfunction

" ==================================================
" neocomplete
" ==================================================

" neocomplcacheを自動起動する
let g:neocomplete#enable_at_startup = 1
" 大文字が入力されるまで大文字小文字を区別しない
let g:neocomplete#enable_smart_case = 1
" シンタックスをキャッシュする際の最小文字長
let g:neocomplete#sources#syntax#min_keyword_length = 3
" バッファ名のパターンを指定する
let g:neocomplete#lock_buffer_name_pattern = '\*ku\*'
" ポップアップで表示される候補の数
let g:neocomplete#max_list = 20
" キャッシュしないファイル名
let g:neocomplete#sources#buffer#disabled_pattern = '\.log\|\.log\.\|\.jax'
" 自動補完を行わないバッファ名
let g:neocomplete#lock_buffer_name_pattern = '\.log\|\.log\.\|.*quickrun.*\|.jax'

" rubyのオムニ補完
if !exists('g:neocomplete#sources#omni#input_patterns')
  let g:neocomplete#sources#omni#input_patterns = {}
endif
let g:neocomplete#sources#omni#input_patterns.ruby = '[^. *\t]\.\h\w*\|\h\w*::'

" ファイルタイプ毎の補完のディクショナリを指定
let g:neocomplcache_dictionary_filetype_lists = {
    \ 'default' : ''
    \ }

" 補完をキャンセルし元に戻す
inoremap <expr><C-g> neocomplete#undo_completion()

" 補完候補の共通部分のみを補完する
inoremap <expr><C-l> neocomplete#complete_common_string()

" タブで候補を選択できるように
" inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
" inoremap <expr><S-TAB>  pumvisible() ? "\<C-p>" : "\<S-TAB>"

" Enterで候補を選択し、改行もされないようにする
function! s:my_crinsert()
    return pumvisible() ? neocomplete#close_popup() : "\<Cr>"
endfunction
inoremap <silent> <CR> <C-R>=<SID>my_crinsert()<CR>

" <C-h>, <BS>補完を禁止
inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"

" ==================================================
" neosnippet
" ==================================================
" Tell Neosnippet about the other snippets
let g:neosnippet#snippets_directory='~/dotfiles/vim/snippets/'

inoremap <expr><S-TAB>  pumvisible() ? "\<C-p>" : "\<S-TAB>"

" Plugin key-mappings.
imap <C-k> <Plug>(neosnippet_expand_or_jump)
smap <C-k> <Plug>(neosnippet_expand_or_jump)

" SuperTab like snippets behavior.
imap <expr><TAB> neosnippet#jumpable() ? "\<Plug>(neosnippet_expand_or_jump)" : pumvisible() ? "\<C-n>" : "\<TAB>"
smap <expr><TAB> neosnippet#jumpable() ? "\<Plug>(neosnippet_expand_or_jump)" : "\<TAB>"

" For snippet_complete marker.
if has('conceal')
  set conceallevel=2 concealcursor=i
endif

" ==================================================
" vim-fugitive
" ==================================================

nnoremap [fugitive] <Nop>
nmap <Space>g [fugitive]

nnoremap <silent> [fugitive]df :Gdiff<CR>
nnoremap <silent> [fugitive]ad :Gwrite<CR>
nnoremap <silent> [fugitive]cv :Gcommit -v<CR>
nnoremap <silent> [fugitive]cam :Gcommit --amend<CR>
nnoremap <silent> [fugitive]bl :Gblame<CR>
nnoremap <silent> [fugitive]st :Gstatus<CR>

" ==================================================
" surround.vim
" ==================================================

" ==================================================
" switch.vim
" ==================================================

" Prefix
nnoremap [switch] <Nop>
nmap <Space>t [switch]

inoremap [switch] :Switch<CR>
nnoremap [switch] :Switch<CR>
vnoremap [switch] :Switch<CR>

" ==================================================
" vim-over
" ==================================================

nnoremap [over] <Nop>
nmap <Space>s [over]

nnoremap <silent> [over]s :OverCommandLine<CR>s/
nnoremap <silent> [over]% :OverCommandLine<CR>%s/
nnoremap <silent> [over]1 :OverCommandLine<CR>1
nnoremap <silent> [over]2 :OverCommandLine<CR>2
nnoremap <silent> [over]3 :OverCommandLine<CR>3
nnoremap <silent> [over]4 :OverCommandLine<CR>4
nnoremap <silent> [over]5 :OverCommandLine<CR>5
nnoremap <silent> [over]6 :OverCommandLine<CR>6
nnoremap <silent> [over]7 :OverCommandLine<CR>7
nnoremap <silent> [over]8 :OverCommandLine<CR>8
nnoremap <silent> [over]9 :OverCommandLine<CR>9

" ==================================================
" vim-rails
" ==================================================

nnoremap [rails] <Nop>
nmap <Space>r [rails]

nnoremap <silent> gp :bp<CR>

" ==================================================
" vim-easymotion
" ==================================================

"Disable default mappings
let g:EasyMotion_do_mapping = 0
" 大文字小文字を区別しない
let g:EasyMotion_smartcase = 1

nmap <Space>e <Plug>(easymotion-s2)

" ==================================================
" vim-easy-align
" ==================================================

xmap <Space>e <Plug>(EasyAlign)

" ==================================================
" vim-lightline
" ==================================================
set laststatus=2
set t_Co=256

let g:lightline = {
  \   'mode_map': {'c': 'NORMAL'},
  \   'active': {
  \     'right': [
  \        [ 'syntastic', 'lineinfo'],
  \        [ 'percent' ],
  \        [ 'fileformat', 'fileencoding', 'filetype' ]
  \      ],
  \     'left':  [ [ 'mode'], ['fugitive'] ]
  \   },
  \   'component_function': {
  \     'modified': 'LightLineModified',
  \     'readonly': 'LightLineReadonly',
  \     'fugitive': 'LightLineFugitive',
  \     'filename': 'LightLineFilename',
  \     'fileformat': 'LightLineFileformat',
  \     'filetype': 'LightLineFiletype',
  \     'mode': 'LightLineMode',
  \   },
  \   'component_expand': {
  \     'syntastic': 'SyntasticStatuslineFlag'
  \   },
  \   'component_type': {
  \     'syntastic': 'error'
  \   }
  \ }

function! LightLineModified()
  return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! LightLineReadonly()
  return &ft !~? 'help\|vimfiler\|gundo' && &readonly ? '🔒 ' : ''
endfunction

function! LightLineFilename()
  return ('' != LightLineReadonly() ? LightLineReadonly() . ' ' : '') .
        \ (&ft == 'vimfiler' ? vimfiler#get_status_string() :
        \  &ft == 'unite' ? unite#get_status_string() :
        \  &ft == 'vimshell' ? vimshell#get_status_string() :
        \ '' != expand('%:t') ? expand('%:t') : '[No Name]') .
        \ ('' != LightLineModified() ? ' ' . LightLineModified() : '')
endfunction

function! LightLineFugitive()
  try
    if &ft !~? 'vimfiler\|gundo' && exists('*fugitive#head')
      return fugitive#head()
    endif
  catch
  endtry
  return ''
endfunction

function! LightLineFileformat()
  return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! LightLineFiletype()
  return winwidth(0) > 70 ? (strlen(&filetype) ? &filetype : 'no ft') : ''
endfunction

function! LightLineFileencoding()
  return winwidth(0) > 70 ? (strlen(&fenc) ? &fenc : &enc) : ''
endfunction

function! LightLineMode()
  return winwidth(0) > 60 ? lightline#mode() : ''
endfunction

" ==================================================
" syntastic
" ==================================================
"  mode に active を指定すると保存するたびに、
"          passiveを指定すると :SyntasticCheck を実行時に検出されます
let g:syntastic_ruby_checkers = [ 'rubocop' ]
let g:syntastic_javascript_checkers = [ 'eslint' ]
let g:syntastic_scss_checkers = ['scss_lint']

augroup AutoSyntastic
  autocmd!
  autocmd BufWritePost * call s:syntastic()
augroup END

function! s:syntastic()
  SyntasticCheck
  call lightline#update()
endfunction

" ==================================================
" Emmet
" ==================================================
let g:user_emmet_expandabbr_key='<c-e>'
